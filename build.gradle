plugins {
  id 'org.inferred.processors' version '2.1.0'
}

ext {
  libVersion = '0.0.25'
}

println("Building version ${libVersion}")

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'org.inferred.processors'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
  }

  configurations {
    deployerJars
  }

  dependencies {
    processor group: 'org.immutables', name: 'value', version: '2.6.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
  }

  jar {
    version libVersion
  }

  eclipse {
      project {
          resourceFilter {
              appliesTo = 'FOLDERS'
              type = 'EXCLUDE_ALL'
              matcher {
                  id = 'org.eclipse.ui.ide.multiFilter'
                  arguments = '1.0-name-matches-false-false-build'
              }
          }
      }
  }
  
  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    version jar.version
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }

  publishing {
    repositories {
      maven {
        name = 'myRepo'
        url = "file://${projectDir}/../repo"
      }
    }
  }
}

